import "./Vars/Global.ostw";
import "./Vars/Player.ostw";
import "./Macros/hostPlayer.ostw";

define propulsionCheck(): buttonHeldJumpOrCrouch() || throttleOfHP() != Vector(0, 0, 0);

rule: "▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒【Josbird's NoClip Module】▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒"
{
}

rule: "Start noclipping"
if (modeIsSet())
if(inEdit())
{
    SetGravity(HP(), 0);
    DisableMovementCollisionWithEnvironment(HP(), true);
    DisableMovementCollisionWithPlayers(HP());
    SetMoveSpeed(HP(), 0);
}

rule: "Stop noclipping"
if (modeIsSet())
if(!inEdit())
{
    SetGravity(HP(), 100);
    EnableMovementCollisionWithEnvironment(HP());
    EnableMovementCollisionWithPlayers(HP());
    SetMoveSpeed(HP(), 100);
}

rule: "Noclip propulsion"
if(inEdit())
if (
    (propulsionCheck()) == true)
{
    ApplyImpulse(HP(), velocityOfHP() * -1, speedOfHP() * 0.03, Relative.ToWorld, ContraryMotion.Incorporate);
    ApplyImpulse(HP(), Normalize(facingDirectionHP() * ZOf(throttleOfHP()) + WorldVectorOf(Left(), HP(), LocalVector.Rotation) * XOf(throttleOfHP()) + Up() * (btnHeld(Button.Jump)) - btnHeld(Button.Crouch)), (60 + 400 * btnHeld(Button.Reload) - speedOfHP()) / 62.5, Relative.ToWorld, ContraryMotion.Incorporate);
    MinWait();
    LoopIfConditionIsTrue();
}

rule: "Noclip air resistance"
if(inEdit())
if (!propulsionCheck())
if (speedOfIsGreaterHP(0.01))
{
    ApplyImpulse(HP(), velocityOfHP() * -1, speedOfHP() * 0.06, Relative.ToWorld, ContraryMotion.Incorporate);
    MinWait();
    LoopIfConditionIsTrue();
}